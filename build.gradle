plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'net.kyori.invent' version '1.0.4'
  id 'net.kyori.invent-javadoc-jar' version '1.0.4'
  id 'net.kyori.invent-sonatype-repository' version '1.0.4'
  id 'net.kyori.invent-sources-jar' version '1.0.4'
  id 'net.minecrell.licenser' version '0.4.1'
}

group 'net.kyori'
description 'Filters.'
version '1.0.0-SNAPSHOT'

tasks.withType(JavaCompile) {
  options.compilerArgs += ['-Xlint:all']
}

license {
  header project.file('header.txt')
  include '**/*.java'
  newLine false
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://oss.sonatype.org/content/groups/public/'
  }
}

dependencies {
  api 'net.kyori:component:1.0.0-SNAPSHOT'
  api 'net.kyori:examination-api:1.0.0-SNAPSHOT'
  api 'org.checkerframework:checker-qual:2.8.2'
  testImplementation 'com.google.guava:guava-testlib:28.0-jre'
  testImplementation 'com.google.truth:truth:0.45'
  testImplementation 'com.google.truth.extensions:truth-java8-extension:0.45'
  testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.2'
  testCompile 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

jar {
  manifest.attributes(
    'Automatic-Module-Name': 'net.kyori.filter'
  )
}

inventSonatypeRepository.enabled = System.getenv('TRAVIS') == null || project.version.endsWith('-SNAPSHOT')

publishing {
  publications {
    maven(MavenPublication) {
      from components.java

      artifact javadocJar
      artifact sourcesJar

      pom {
        name = project.name
        description = project.description

        developers {
          developer {
            id = 'kashike'
          }
        }

        issueManagement {
          system = 'GitHub Issues'
          url = 'https://github.com/KyoriPowered/filter/issues'
        }

        licenses {
          license {
            name = 'MIT License'
            url = 'https://opensource.org/licenses/MIT'
          }
        }

        scm {
          connection = 'scm:git@github.com:KyoriPowered/filter.git'
          developerConnection = 'scm:git@github.com:KyoriPowered/filter.git'
          url = 'https://github.com/KyoriPowered/filter/'
        }
      }
    }
  }
}

signing {
  required { project.hasProperty('signing.keyId') && gradle.taskGraph.hasTask(':publish') && project.version.endsWith('-SNAPSHOT') }
  sign publishing.publications.maven
}
